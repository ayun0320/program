; $ID: read_flambe_hour_TA_dn.pro V01 03/26/2012 09:14 BRUCE EXP$
;
;******************************************************************************
;  PROCEDURE read_flambe_hour_TA_dn READS THE FIRE INFO FROM FLAMBE FIRE EMISSION 
;  DATA WITH BOTH SATELLITE SOURCES (TERRA AND AQUA) WITH DAY AND NIGHT TIME. 
;  INFO INCLUDES LATITUDE, LONGITUDE, AND SATELLITE CODE.
;  SATELLITE CODES AS FOLLOWING:
;  (1 ) 250 = GOES-10 = GOES-WEST
;  (2 ) 251 = GOES-11 = GOES-WEST
;  (3 ) 252 = GOES-12 = GOES-EAST
;  (4 ) 783 = MODIS-Terra
;  (5 ) 784 = MODIS-Aqua
;
;  VARIABLES:
;  ============================================================================
;  (1 ) FILEDIR   (STRING) : FLAMBE FIRE FILE DIRECTORY                   [---]
;  (2 ) FILELIST  (STRING) : FLAMBE FIRE FILE NAME LIST                   [---]
;  (3 ) YEAR      (INTEGER): YEAR OF MODIS LEVEL 1B DATA                  [---]
;  (4 ) MONTH     (INTEGER): MONTH OF MODIS LEVEL 1B DATA                 [---]
;  (5 ) DAY       (INTEGER): DAY OF MODIS LEVEL 1B DATA                   [---]
;  (6 ) SATELLITE1(INTEGER): SATELLITE1 CODE(TERRA)                       [---]
;  (7 ) SATELLITE2(INTEGER): SATELLITE2 CODE(AQUA)                        [---]
;  (8 ) DTFIRELAT (FLOAT)  : LATITUDE OF FIRE DOTS FROM DAY TIME TERRA    [Deg]
;  (9 ) DTFIRELON (FLOAT)  : LONGITUDE OF FIRE DOTS FROM DAY TIME TERRA   [Deg]
;  (10) NTFIRELAT (FLOAT)  : LATITUDE OF FIRE DOTS FROM NIGHT TIME TERRA  [Deg]
;  (11) NTFIRELON (FLOAT)  : LONGITUDE OF FIRE DOTS FROM NIGHT TIME TERRA [Deg]
;  (12) DAFIRELAT (FLOAT)  : LATITUDE OF FIRE DOTS FROM DAY TIME AQUA     [Deg]
;  (13) DAFIRELON (FLOAT)  : LONGITUDE OF FIRE DOTS FROM DAY TIME AQUA    [Deg]
;  (14) NAFIRELAT (FLOAT)  : LATITUDE OF FIRE DOTS FROM NIGHT TIME AQUA   [Deg]
;  (15) NAFIRELON (FLOAT)  : LONGITUDE OF FIRE DOTS FROM NIGHT TIME AQUA  [Deg]
;
;  NOTES:
;  ============================================================================
;  (1 ) ORIGINALLY WRITTEN BY BRUCE. (03/22/2012)
;  (2 ) MODIFIED FROM read_flambe.pro. (03/23/2012)
;  (3 ) MODIFIED FROM read_flambe_hour.pro. (03/26/2012)
;  (4 ) MODIFIED FROM read_flambe_hour_TA_dn.pro. (03/27/2012)
;******************************************************************************

PRO READ_FLAMBE_HOUR_TA_dn, FILEDIR, FILELIST, YEAR = YEAR,           $
                         MONTH = MONTH, DAY = DAY, HOUR = HOUR,       $
                         SATELLITE1 = SATELLITE1,                     $
                         SATELLITE2 = SATELLITE2,                     $
                         DTFIRELAT = DTFIRELAT, DTFIRELON = DTFIRELON,$
                         NTFIRELAT = NTFIRELAT, NTFIRELON = NTFIRELON,$
                         DAFIRELAT = DAFIRELAT, DAFIRELON = DAFIRELON,$
                         NAFIRELAT = NAFIRELAT, NAFIRELON = NAFIRELON

;  READ FLAMBE FIRE EMISSION FILE NAMES
   READCOL, FILEDIR + FILELIST, FILENAMES, FORMAT = 'A'

;  SELECT THE FIRE FILE NAMES WITH THE SAME YEAR, MONTH, AND DAY AS THE MODIS'S
   NFILE = N_ELEMENTS(FILENAMES)
   FYEAR = INTARR(NFILE)
   FMONTH= INTARR(NFILE)
   FDAY  = INTARR(NFILE)
   FHOUR = INTARR(NFILE)
   FOR I = 0, NFILE-1 DO BEGIN
    FYEAR(I) = FIX(STRMID(FILENAMES(I), 14, 4))
    FMONTH(I)= FIX(STRMID(FILENAMES(I), 18, 2))
    FDAY(I)  = FIX(STRMID(FILENAMES(I), 20, 2))
    FHOUR(I) = FIX(STRMID(FILENAMES(I), 22, 2))
   ENDFOR
   IND   = WHERE(FYEAR  EQ YEAR  AND $
                 FMONTH EQ MONTH AND $
                 FDAY   EQ DAY   AND $
                 FHOUR  EQ HOUR, NIND)
;  DO THE LOOP OF NIND TO READ SELECTED FLAMBE FIRE EMISSION
   DTLINE = 0
   NTLINE = 0
   DALINE = 0
   NALINE = 0
   FOR I = 0, NIND-1 DO BEGIN
    READCOL, FILEDIR + FILENAMES(IND(I)), ADATE, AFIRELON, AFIRELAT, $
             AINJ_MIN, AINJ_MED, AINJ_MAX, ASATELLITE, AAREA,        $
             ARATE, ATIME, ACARBON, AEXT_1, AEXT_2, AEXT_3,          $
             FORMAT = 'A, F, F, I, I, I, I, F, F, I, F, F, F, F'
;  EXTRACT THE EXACT YEAR, MONTH, DAY, HOUR FROM THE ADATE
    NLINE = N_ELEMENTS(ADATE)
    AYEAR = INTARR(NLINE)
    AMONTH= INTARR(NLINE)
    ADAY  = INTARR(NLINE)
    AHOUR = INTARR(NLINE)
    FOR J = 0, NLINE-1 DO BEGIN
     AYEAR(J) = FIX(STRMID(ADATE(J), 0, 4))
     AMONTH(J)= FIX(STRMID(ADATE(J), 4, 2))
     ADAY(J)  = FIX(STRMID(ADATE(J), 6, 2))
     AHOUR(J) = FIX(STRMID(ADATE(J), 8, 2))
    ENDFOR
;  DISTINGUISH DAY AND NIGHT FIRE DOTS
;  FOR TERRA
;  JUDGE DAY TIME
    DTIND= WHERE(ASATELLITE EQ SATELLITE1 AND $
                 AHOUR GT 6 AND AHOUR LT 14, NDTIND)
    IF (NDTIND GT 0) THEN BEGIN
     DTFIRELAT(DTLINE:DTLINE+NDTIND-1) = AFIRELAT(DTIND)
     DTFIRELON(DTLINE:DTLINE+NDTIND-1) = AFIRELON(DTIND)
     DTLINE = DTLINE + NDTIND
    ENDIF
;  JUDGE NIGHT TIME
    NTIND= WHERE(ASATELLITE EQ SATELLITE1 AND $
                 AHOUR GE 19 AND AHOUR LE 23, NNTIND)
    IF (NNTIND GT 0) THEN BEGIN
     NTFIRELAT(NTLINE:NTLINE+NNTIND-1) = AFIRELAT(NTIND)
     NTFIRELON(NTLINE:NTLINE+NNTIND-1) = AFIRELON(NTIND)
     NTLINE = NTLINE + NNTIND
    ENDIF
;  FOR AQUA
;  JUDGE DAY TIME
    DAIND= WHERE(ASATELLITE EQ SATELLITE2 AND $
                 AHOUR GT 9 AND AHOUR LT 16, NDAIND)
    IF (NDAIND GT 0) THEN BEGIN
     DAFIRELAT(DALINE:DALINE+NDAIND-1) = AFIRELAT(DAIND)
     DAFIRELON(DALINE:DALINE+NDAIND-1) = AFIRELON(DAIND)
     DALINE = DALINE + NDAIND
    ENDIF
;  JUDGE NIGHT TIME
    NAIND1= WHERE(ASATELLITE EQ SATELLITE2 AND  $
                  AHOUR GE 22, NNAIND1)
    IF (NNAIND1 GT 0) THEN BEGIN
     NAFIRELAT(NALINE:NALINE+NNAIND1-1) = AFIRELAT(NAIND1)
     NAFIRELON(NALINE:NALINE+NNAIND1-1) = AFIRELON(NAIND1)
     NALINE = NALINE + NNAIND1
    ENDIF
    NAIND2= WHERE(ASATELLITE EQ SATELLITE2 AND  $
                  AHOUR GE 0 AND AHOUR LE 5, NNAIND2)
    IF (NNAIND2 GT 0) THEN BEGIN
     NAFIRELAT(NALINE:NALINE+NNAIND2-1) = AFIRELAT(NAIND2)
     NAFIRELON(NALINE:NALINE+NNAIND2-1) = AFIRELON(NAIND2)
     NALINE = NALINE + NNAIND2
    ENDIF
   ENDFOR

END

