; $Id: inv_index.pro,v 1.1.1.1 2007/07/17 20:41:40 bmy Exp $
;-----------------------------------------------------------------------
;+
; NAME:
;        INV_INDEX
;
; PURPOSE:
;        Find the indices that do NOT match a WHERE condition
;
; CATEGORY:
;        General
;
; CALLING SEQUENCE:
;        RESULT = INV_INDEX( INDEX, TOTALN )
;
; INPUTS:
;        INDEX -> An index array, e.g. previously generated by a
;              WHERE command (may be -1)
;
;        TOTALN -> the number of elements in the reference data
;              set, i.e. totaln = n_elements(index)+n_elements(result)
;
; KEYWORD PARAMETERS:
;        None
;
; OUTPUTS:
;        RESULT -> an integer array with all indices that were NOT
;              in index or -1 if index was complete
;
; SUBROUTINES:
;        None
;
; REQUIREMENTS:
;        None
;
; NOTES:
;        The function returns -1 if one of the following errors occurs:
;        - invalid number of arguments
;        - index variable is undefined
;        - totaln is less than n_elements(index)
;        - totaln less or equal 1, i.e. no associated data
;        The last error does not produce an error message, since this
;        feature was found to be very useful (in EXPLORE, the widget based
;        interactive data explorer)
;
; EXAMPLE:
;        DATA   = FINDGEN( 50 )
;        INDEX  = WHERE( DATA ge 25 )
;        INVERS = INV_INDEX( INDEX, N_ELEMENTS( DATA ) )
;        PRINT, INVERS
;           0  1  2  3  4  5  6  7  8  9
;          10 11 12 13 14 15 16 17 18 19
;          20 21 22 23 24
;
;             ; Find the elements of DATA that are greater than 25
;             ; and then print the inverse condition
;
; MODIFICATION HISTORY:
;        mgs, 10 May 1997: VERSION 1.00
;        mgs, 18 Aug 1997: - added template and check if n_elements(index) eq 0
;        mgs, 05 Apr 1999: - bug fix: needed to make sure result is type long
;  bmy & phs, 13 Jul 2007: GAMAP VERSION 2.10
;
;-
; Copyright (C) 1997-2007, Martin Schultz,
; Bob Yantosca and Philippe Le Sager, Harvard University
; This software is provided as is without any warranty whatsoever. 
; It may be freely used, copied or distributed for non-commercial 
; purposes. This copyright notice must be kept with any copy of 
; this software. If this software shall be used commercially or 
; sold as part of a larger package, please contact the author.
; Bugs and comments should be directed to bmy@io.as.harvard.edu
; or phs@io.harvard.edu with subject "IDL routine inv_index"
;-----------------------------------------------------------------------


function inv_index,index,totaln

   newindex = -1L     ; default: nothing left

   ; check for errors:
   if (N_Params() ne 2) then begin
      print,'INV_INDEX: wrong number of arguments'
      return,newindex
   endif
   if (n_elements(index) eq 0) then begin
      print,'INV_INDEX: no valid index passed'
      return,newindex
   endif
   if (totaln lt n_elements(index)) then begin
      print,'INV_INDEX: totaln lt n_elements(index)'
      return,newindex
   endif
   if (totaln le 1) then return,newindex    ; no data there

   ; and handle the two situations:
   if (max(index) lt 0) then begin   ; no valid index passed
      newindex = lindgen(totaln)      ; create an integer array
      return,newindex                ; with totaln elements
   endif

   ; else a valid indexarray was passed and we can construct the inverse
   newindex = lindgen(totaln)
   newindex(index) = -1
   i = where(newindex ge 0,count)
   if (count gt 0) then newindex = newindex(i)  $
   else newindex = -1L

   return, newindex
end


